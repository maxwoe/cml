grammar at.ac.univie.swa.Cml with org.eclipse.xtext.common.Terminals

import 'http://www.eclipse.org/emf/2002/Ecore' as ecore

generate cml 'http://swa.univie.ac.at/Cml'

Contract:
	'contract' name=ID
	BEGIN
		(parties+=Party |
		 events+=Event |
		 attributes+=Attribute |
		 types+=ComplexType |
		 clauses+=Clause)*
	END;

Classifier:
	Feature | Record | Entity | Party | Event | Clause;
	
Feature:
    Attribute | Action;
	
Party:
	'party' name=ID ':' many?='many'?  type=[Entity|FQN];

Action:
	visibility=Visibility? name=ID '(' (args+=Attribute (',' args+=Attribute)*)? ')' (':' returnType=AttributeType)?;

enum Visibility:
	PUBLIC='public' | PRIVATE='private' | PROTECTED='protected';
		
AttributeType:
	/*(collection = Collection)?*/ many?='many'? type=Type (array ?='[' (length=INT)? ']')?;

Collection:
	{Set} 'set' | {Bag} 'bag';
	
Type:
	SimpleType | ComplexTypeRef;
	
SimpleType:
	{IntegerType} 'integer' | {BooleanType} 'boolean' | {StringType} 'string' | {DateType} 'date';

ComplexType:
	Record | Entity;
	
ComplexTypeRef:
	ref=[ComplexType|FQN];

Entity:
	'entity' name=ID ('extends' superType=[Entity|FQN])?
	BEGIN
		(attributes+=Attribute |
		 types+=ComplexType |
		 actions+=Action)*
	END;
	
Record:
	'datatype' name=ID
	BEGIN
		attributes+=Attribute+
	END;
	  
Attribute:
	name=ID ':' typeDef=AttributeType ('=' expr=Expression)?;

Event:
	'event' name=ID ('(' (args+=Attribute (',' args+=Attribute)*)? ')')?;

Clause:
	'clause' name=ID
	BEGIN
		antecedent=Antecedent
		actor=Actor
		action=DeonticAction
		reparation=Reparation?
	END;

Antecedent:
	{Antecedent} temporal=TemporalConstraint? general=Constraint?;

TemporalConstraint:
	'due' constraint=TimeConstraint;
	
Constraint:
	'given' expression=Expression;

Actor:
	'party' party=[Party|FQN];

enum Deontic:
	MUST='must' | MAY='may' | MUST_NOT= 'must_not';

DeonticAction:
	deontic=Deontic compoundAction=CompoundAction;

CompoundAction:
	OrCompoundAction;

OrCompoundAction returns CompoundAction:
	SeqCompoundAction ({OrCompoundAction.left=current} op=('or') right=SeqCompoundAction)*;

SeqCompoundAction returns CompoundAction:
	AndCompoundAction ({SeqCompoundAction.left=current} op=('then') right=AndCompoundAction)*;
	
AndCompoundAction returns CompoundAction:
	AtomicCompoundAction ({AndCompoundAction.left=current} op=('and') right=AtomicCompoundAction)*;

AtomicCompoundAction returns CompoundAction:
	'(' CompoundAction ')' | AtomicAction;

AtomicAction:
	action=[Action|FQN] '(' (args+=[Attribute|FQN] (',' args+=[Attribute|FQN])*)? ')'
	(BEGIN
		'when' preCondition=[Attribute]
		('then' postCondition=Expression)?
	END)?
	;
	
Reparation:
	'otherwise' referTo=[Clause|FQN];

TimeConstraint:
	closed?='within'? timeframe=(RelativeTime | DotExpression)? period=PeriodicTime? precedence=TemporalPrecedence reference=(AbsoluteTime | DotExpression);

PeriodicTime:
	'every' period=(RelativeTime | DotExpression) 'from' start=(AbsoluteTime | DotExpression) ('until' end=(AbsoluteTime | DotExpression))?;
	
enum TemporalPrecedence:
	BEFORE='before' |
	//BEFORE_OR_EQUALS='before or at the same time as' |
	//EQUALS ='at the same time as' |
	//AFTER_OR_EQUALS='after or at the same time as' |
	AFTER='after';

ClauseQuery:
	'clause' ref=[Clause|FQN] 'fulfilled';

EventQuery:
	'event' ref=[Event|FQN] 'occurred';
	
ActionQuery:
	multiplicity=('all'|'any'|'current')?  partyRef=[Party|FQN] query='executed' actionRef=[Action|FQN];
	
TimeQuery:
	ClauseQuery | EventQuery | ActionQuery;
	
Time:
	AbsoluteTime | RelativeTime;

AbsoluteTime:
	TimeQuery | globalTimeRef=GlobalTimeReference;
	
RelativeTime:
	TimeFrame;

enum GlobalTimeReference:
	NOW='$now' | CONTRACT_START='$contractStart' | CONTRACT_END='$contractEnd';
	
TimeFrame:
	{TimeFrame} (weeks=INT 'weeks') | {TimeFrame} (days=INT 'days') | {TimeFrame} (hours=INT 'hours') | {TimeFrame} (minutes=INT 'minutes') | {TimeFrame} (seconds=INT 'seconds');
	
Expression:
	Assignment;

Assignment returns Expression:
	Implies ({Assignment.left=current} '=' right=Implies)*;

Implies returns Expression:
	Conditional ({Implies.left=current} op=('=>' | 'implies') right=Conditional)*; 

Conditional returns Expression:
	Or ({Conditional.left=current} '?' trueCase=Or ':' falseCase=Or)?;// |
	//'if' Or ({Conditional.left=current} 'then' trueCase=Or 'else' falseCase=Or)? ;
  
Or returns Expression:
	And ({Or.left=current} op=('||' | 'or') right=And)*;

And returns Expression:
	Equality ({And.left=current} op=('&&' | 'and') right=Equality)*;

Equality returns Expression:
	Relation ({Equality.left=current} op=('==' | '!=') right=Relation)*;

Relation returns Expression:
	PlusOrMinus ({Relation.left=current} op=('>=' | '<=' | '>' | '<') right=PlusOrMinus)*;

PlusOrMinus returns Expression:
	MulOrDiv ({PlusOrMinus.left=current} op=('+' | '-') right=MulOrDiv)*;

MulOrDiv returns Expression:
	Unary ({MulOrDiv.left=current} op=('*' | '/') right=Unary)*;

Unary returns Expression:
	Postfix | {Unary} op=('+' | '-') right=Postfix;

Postfix returns Expression:
	Primary ({Postfix.left=current} op=('++'|'--'))*;

Primary returns Expression:
	'(' Expression ')' | {Not} op=('!' | 'not') expression=Primary | Atomic;

Atomic returns Expression:
	{IntegerConstant} value=INT | 
	{StringConstant} value=STRING | 
	{BooleanConstant} value=('true' | 'false') |
	ref=[Attribute];
	
DotExpression returns Ref:
    DotExpressionStart ({DotExpression.head=current} '.' tail=[Attribute])*;
 
DotExpressionStart returns Ref:
    {DotExpressionStart} ref=[Attribute];

DotReference:
	Party | Attribute;
	
/*
enum BuiltinConstants:
	  questionMark='?'
	| self='self'
	| const_e
	| pi
;*/
FQN:
	ID ('.' ID)*;
	
//terminal BOOL returns ecore::EBoolean:
//	'true' | 'false';
//terminal NUMBER returns ecore::EBigDecimal:
//	/*('-')?*/ ('0'..'9')* ('.' ('0'..'9')+)?;
terminal BEGIN: 'synthetic:BEGIN';  // increase indentation
terminal END: 'synthetic:END';      // decrease indentation
