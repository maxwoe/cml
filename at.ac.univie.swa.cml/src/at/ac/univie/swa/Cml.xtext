grammar at.ac.univie.swa.Cml with org.eclipse.xtext.common.Terminals

import "http://www.eclipse.org/emf/2002/Ecore" as ecore

generate cml "http://swa.univie.ac.at/Cml"

Contract:
	'contract' name=ID
	BEGIN
		(parties+=Party |
		 attributes+=Attribute |
		 entities+=Entity |
		 events+=Event |
		 clauses+=Clause)*
	END;

Party:
	'party' name=ID
	BEGIN
		(attributes+=Attribute | 
		 actions+=Action)*
	END;

Action:
	/*visibility=Visibility? returnType=[PrimitiveType]?*/ name=ID '(' (args+=Attribute (',' args+=Attribute)*)? ')';
	
AbstractType:
	PrimitiveType | EntityType;
	
PrimitiveType:
	{IntegerType} "integer" | {BooleanType} "boolean" | {StringType} "string" | {DateType} "date";

EntityType:
	ref=[Entity|ID];

Entity:
	'entity' name=ID /*('extends' superType=[Entity|ID])?*/
	BEGIN
		(attributes+=Attribute)* 
	END;

Attribute:
	(many?='many')? name=ID (((":" type=AbstractType)? "=" expr=Expression) | (":" type=AbstractType));

Event:
	'event' name=ID;

Clause:
	'clause' name=ID
	BEGIN
		antecedent=Antecedent
		actor=Actor
		action=DeonticAction
		reparation=Reparation?
	END;

Antecedent:
	{Antecedent} temporal=TemporalConstraint? general=Constraint?;

TemporalConstraint:
	'due' constraint=TimeConstraint;
	
Constraint:
	'given' expression=Expression;

Actor:
	'party' party=[Party|ID];

enum Deontic:
	MUST='must' | MAY='may' | MUST_NOT= 'must_not';

DeonticAction:
	deontic=Deontic action=[Action|ID]'(' (args+=[Attribute] (',' args+=[Attribute])*)? ')';

Reparation:
	'otherwise' referTo=[Clause|ID];

TimeConstraint:
	OpenTimeConstraint | => ClosedTimeConstraint;

OpenTimeConstraint:
	precedence=TemporalPrecedence reference=(AbsoluteTime | DotExpression);
	
ClosedTimeConstraint:
	closed?='within'? timeframe=(RelativeTime | DotExpression) precedence=('before' | 'after') reference=(AbsoluteTime | DotExpression);

enum TemporalPrecedence:
	BEFORE='before' | AFTER='after';

//TimeQuery:
//	/*ClauseQuery | EventQuery |*/ AbstractTimeQuery | GlobalTimeQuery | DotExpression;

/*
ClauseQuery:
	ref=[Clause] 'fulfilled';

EventQuery:
	ref=[Event] 'occurred';*/

TimeQuery:
	ref=[TimeQuerySource|ID] 'done';
	
TimeQuerySource:
	Clause | Event | Action;
	
Time:
	AbsoluteTime | RelativeTime;

AbsoluteTime:
	TimeQuery | globalTimeRef=GlobalTimeReference;
	
RelativeTime:
	TimeFrame;

enum GlobalTimeReference:
	NOW='now' | CONTRACT_START='contractStart';
	
TimeFrame:
	{Weeks} (weeks=INT 'weeks') | {Days} (days=INT 'days') | {Hours} (hours=INT 'hours') | {Minutes} (minutes=INT 'minutes') | {Seconds} (seconds=INT 'seconds');
	
Expression:
	Assignment;

Assignment returns Expression:
	Conditional ({Assignment.left=current} '=' right=Conditional)*;

Conditional returns Expression:
	Or ({Conditional.left=current} '?' trueCase=Or ':' falseCase=Or)? |
	'if' Or ({Conditional.left=current} 'then' trueCase=Or 'else' falseCase=Or)? ;
	
Or returns Expression:
	And ({Or.left=current} op=('||' | 'or') right=And)*;

And returns Expression:
	Equality ({And.left=current} op=('&&' | 'and') right=Equality)*;

Equality returns Expression:
	Relation ({Equality.left=current} op=('==' | '!=') right=Relation)*;

Relation returns Expression:
	PlusOrMinus ({Relation.left=current} op=('>=' | '<=' | '>' | '<') right=PlusOrMinus)*;

PlusOrMinus returns Expression:
	MulOrDiv ({PlusOrMinus.left=current} op=('+' | '-') right=MulOrDiv)*;

MulOrDiv returns Expression:
	Unary ({MulOrDiv.left=current} op=('*' | '/') right=Unary)*;

Unary returns Expression:
	Postfix | {Unary} op=('+' | '-') right=Postfix;

Postfix returns Expression:
	Primary ({Postfix.left=current} op=('++'|'--'))*;
	
Primary returns Expression:
	'(' Expression ')' | {Not} op=('!' | 'not') expression=Primary | Atomic;

Atomic returns Expression:
	{IntegerConstant} value=INT | 
	{StringConstant} value=STRING | 
	{BooleanConstant} value=('true' | 'false')/* | 
	{TimeExpression} value=TimeFrame  | 
	{ClauseRef} value=[Clause] 'fulfilled'| */
	|DotExpression
	|Time;

FQN:
	ID ('.' ID)*;

DotExpression returns Ref:
    DotExpressionStart ({DotExpression.head=current} "." tail=[Attribute])*;
 
DotExpressionStart returns Ref:
    {DotExpressionStart} ref=[Reference];

Reference:
	Party | Attribute;
	 
//terminal BOOL returns ecore::EBoolean:
//	'true' | 'false';

//terminal NUMBER returns ecore::EBigDecimal:
//	/*("-")?*/ ('0'..'9')* ('.' ('0'..'9')+)?;

@Override 
terminal SL_COMMENT:
	'#' !('\n' | '\r')* ('\r'? '\n')?;
	
// The following synthetic tokens are used for the indentation-aware blocks
terminal BEGIN: 'synthetic:BEGIN';  // increase indentation
terminal END: 'synthetic:END';      // decrease indentation
