grammar at.ac.univie.swa.Cml with org.eclipse.xtext.common.Terminals

import "http://www.eclipse.org/emf/2002/Ecore" as ecore
generate cml 'http://swa.univie.ac.at/Cml'

CmlProgram:
	('namespace' name=QualifiedName)?
	imports+=Import*
	declarations+=Declaration*;

Import:
	'import' importedNamespace=QualifiedNameWithWildcard;

Declaration:
	CmlClass | Party | Asset | Transaction | Event | Contract | Enumeration | Attribute | Operation |
	AnnotationDeclaration;

CmlClass:
	(abstract?='abstract')? kind='concept' name=ID ('<' typeVars+=TypeVariable (',' typeVars+=TypeVariable)* '>')? ('extends' superclass=TypeReference)?
	(BEGIN
	features+=FeatureAttributeAndOperation*
	END)?;

Contract returns CmlClass:
	annotations+=Annotation*
	(abstract?='abstract')? kind='contract' name=ID ('extends' superclass=TypeReference)?
	(BEGIN
	features+=FeatureAttributeAndOperationAndClause*
	END)?;

Party returns CmlClass:
	(abstract?='abstract')? kind='party' name=ID ('extends' superclass=TypeReference)?
	(BEGIN
	features+=FeatureAttribute+
	END)?;

Asset returns CmlClass:
	(abstract?='abstract')? kind='asset' name=ID ('extends' superclass=TypeReference)?
	(BEGIN
	features+=FeatureAttribute+
	END)?;

Transaction returns CmlClass:
	(abstract?='abstract')? kind='transaction' name=ID ('extends' superclass=TypeReference)?
	(BEGIN
	features+=FeatureAttribute+
	END)?;

Event returns CmlClass:
	kind='event' name=ID
	(BEGIN
	features+=FeatureAttribute+
	END)?;

Enumeration returns CmlClass:
	kind='enum' name=ID
	BEGIN
	features+=FeatureEnumeration+
	END;

EnumerationElement:
	name=ID;

AnnotationDeclaration:
	'@' 'interface' name=ID
	(BEGIN
	features+=Attribute+
	END)?;

Annotation:
	'@' declaration=[AnnotationDeclaration|QualifiedName] ('(' (args+=(AnnotationElement) (',' args+=AnnotationElement)*)?
	')')?;

AnnotationElement:
	param=[Attribute] '=' value=Expression;

Type:
	CmlClass;
	
TypeReference:
	ParameterizedTypeReference =>({GenericArrayTypeReference.componentType=current} '[' ']')*;

ParameterizedTypeReference:
	type=[DeclaredType] (=>'<' typeArgs+=TypeReference (',' typeArgs+=TypeReference)* '>' )?;

DeclaredType:
	TypeTypeRef | TypeVarRef;
	
TypeVarRef returns ParameterizedTypeReference:
	type=[TypeVariable];

TypeTypeRef returns ParameterizedTypeReference:
	type=[Type|QualifiedName];
	
TypeVariable:
	name=ID;
			
NamedElement:
	CmlProgram | CmlClass | Feature | VariableDeclaration | AnnotationDeclaration;

Feature:
	Attribute | Operation | Clause | EnumerationElement;

FeatureAttribute returns Feature:
	Attribute;

FeatureAttributeAndOperation returns Feature:
	Attribute | Operation;

FeatureAttributeAndOperationAndClause returns Feature:
	FeatureAttributeAndOperation | Clause;

FeatureEnumeration returns Feature:
	EnumerationElement;

Attribute:
	(constant?='constant')? type=TypeReference name=ID ('=' expression=Expression)?;

Operation:
	'fun' (type=TypeReference)? name=ID
	'(' (params+=Attribute (',' params+=Attribute)*)? ')'
	(body=Block)?;

Clause:
	'clause' name=ID
	BEGIN
	constraint=Constraint
	actor=Actor
	action=DeonticAction
	END;

Constraint:
	{Constraint} temporal=TemporalConstraint? general=GeneralConstraint?;

TemporalConstraint:
	'due' timeframe=Timeframe? period=PeriodicTime? precedence=TemporalPrecedence reference=(TimeQuery
	| Expression);

Timeframe:
	'within' window=Expression;
	
GeneralConstraint:
	'given' expression=Expression;

Actor:
	'party' party=[Attribute];

enum Deontic:
	MUST='must' | MAY='may' /* | MUST_NOT='must_not'*/;

DeonticAction:
	deontic=Deontic compoundAction=CompoundAction;

CompoundAction:
	XorCompoundAction;

XorCompoundAction returns CompoundAction:
	OrCompoundAction (=> ({XorCompoundAction.left=current} op='xor') right=OrCompoundAction)*;

OrCompoundAction returns CompoundAction:
	AndCompoundAction ({OrCompoundAction.left=current} op=('or') right=AndCompoundAction)*;

AndCompoundAction returns CompoundAction:
	PrimaryCompoundAction ({AndCompoundAction.left=current} op=('and') right=PrimaryCompoundAction)*;

PrimaryCompoundAction returns CompoundAction:
	{AtomicAction} operation=[Operation] |
	{NestedCompoundAction} '(' child=CompoundAction ')';

PeriodicTime:
	'every' period=Expression 'from' start=(TimeQuery | Expression) ('until' end=(TimeQuery | Expression))?;

enum TemporalPrecedence:
	BEFORE='before' |
	/*BEFORE_OR_EQUALS='onOrBefore' |
	EQUALS='on' |
	AFTER_OR_EQUALS='onOrAfter' |*/
	AFTER='after';

ClauseQuery:
	'clause' clause=[Clause] status=ClauseStatus;

enum ClauseStatus:
	FULFILLED='fulfilled' |
	FAILED='failed';
	
EventQuery:
	'event' event=[Attribute];

ActionQuery:
	party=[Attribute] query='did' action=[Operation];

TimeQuery:
	ClauseQuery | EventQuery | ActionQuery;

Block:
	{Block} BEGIN statements+=Statement* END;

Statement:
	VariableDeclaration |
	IfStatement |
	ThrowStatement |
	ReturnStatement |
	SwitchStatement |
	WhileStatement |
	DoWhileStatement |
	ForStatement |
	Expression;

VariableDeclaration:
	'var' type=TypeReference name=ID '=' expression=Expression;

ReturnStatement:
	{ReturnStatement} 'return' (=> expression=Expression)?;

IfStatement:
	'if' ('(' condition=Expression ')' | => condition=Expression) thenBlock=ConditionalBlock
	(=> 'else' elseBlock=ConditionalBlock)?;

ConditionalBlock returns Block:
	statements+=Statement |
	Block;

ThrowStatement:
	'throw' expression=Expression;

SwitchStatement:
	'match' ('(' declaration=Expression ')' | => declaration=Expression)
	BEGIN
	(cases+=CasePart)*
	(default?='default' ':' defaultBlock=ConditionalBlock)?
	END;

CasePart:
	'case' case=Expression (':' thenBlock=ConditionalBlock /* | fallThrough?=','*/);

WhileStatement:
	'while' ('(' condition=Expression ')' | => condition=Expression) block=ConditionalBlock;

DoWhileStatement:
	'do' block=ConditionalBlock 'while' ('(' condition=Expression ')' | => condition=Expression);

ForStatement:
	'for' '(' declaration=VariableDeclaration ';' condition=Expression ';' progression=Expression ')' block=Block;

Expression:
	AssignmentExpression;

AssignmentExpression returns Expression:
	OrExpression (=> ({AssignmentExpression.left=current} op=('=' | '+=' | '-=') right=Expression))?;
	
OrExpression returns Expression:
	AndExpression (=> ({OrExpression.left=current} op='or') right=AndExpression)*;

AndExpression returns Expression:
	EqualityExpression (=> ({AndExpression.left=current} op='and') right=EqualityExpression)*;

EqualityExpression returns Expression:
	RelationalExpression (=> ({EqualityExpression.left=current} op=('==' | '!=')) right=RelationalExpression)*;

RelationalExpression returns Expression:
	OtherOperatorExpression
	(=> ({InstanceOfExpression.expression=current} 'is') type=TypeReference |
	=> ({RelationalExpression.left=current} op=('>' | '<' | '>=' | '<=')) right=OtherOperatorExpression)*;

OtherOperatorExpression returns Expression:
	AdditiveExpression (=> ({OtherOperatorExpression.left=current} op='=>') right=AdditiveExpression)*;

AdditiveExpression returns Expression:
	MultiplicativeExpression (=> ({AdditiveExpression.left=current} op=('+' | '-')) right=MultiplicativeExpression)*;

MultiplicativeExpression returns Expression:
	UnaryExpression (=> ({MultiplicativeExpression.left=current} op=('*' | '/' | '**' | '%')) right=UnaryExpression)*;

UnaryExpression returns Expression:
	{UnaryExpression} op=('not' | '!' | '-' | '+') operand=UnaryExpression | CastedExpression;

CastedExpression returns Expression:
	PostfixExpression (=> ({CastedExpression.target=current} 'as') type=TypeReference)*;

PostfixExpression returns Expression:
	FeatureSelectionExpression
	(=> ({PostfixExpression.operand=current} op=("++" | "--" | "@pre")) |
	=> ({ArrayAccessExpression.array=current}
	'[' indexes+=Expression ']' (=> '[' indexes+=Expression ']')*))?;

FeatureSelectionExpression returns Expression:
	PrimaryExpression
	(=> ({FeatureSelectionExpression.receiver=current} ("." | explicitStatic?="::")) feature=[Feature]
	(=> opCall?='(' (args+=Expression (',' args+=Expression)*)? ')')?)*;

PrimaryExpression returns Expression:
	LiteralExpression |
	{ThisExpression} 'this' |
	{SuperExpression} 'super' |
	{ReferenceExpression} reference=[NamedElement] (=> opCall?='(' (args+=Expression (',' args+=Expression)*)? ')')? 
	(=>({ArrayAccessExpression.array=current} '[' indexes+=Expression ']' (=>'[' indexes+=Expression ']')*))?|
	{NewExpression} 'new' type=[Type|QualifiedName] '(' (args+=Expression (',' args+=Expression)*)? ')' |
	{NestedExpression} '(' child=Expression ')';

LiteralExpression returns Expression:
	{IntegerLiteral} value=INT |
	{BooleanLiteral} value=('false' | 'true') |
	{StringLiteral} value=STRING |
	{RealLiteral} value=REAL |
	{DurationLiteral} value=INT unit=TimeUnit |
	{DateTimeLiteral} value=DATE |
	{NullLiteral} 'null' |
	{ArrayLiteral} '{' (elements+=Expression (',' elements+=Expression )*)? '}' |
	{Closure} '[' expression=ExpressionInClosure ']' ;

ExpressionInClosure returns Expression:
	{Block} (expressions+=Expression ';'?)*;

enum TimeUnit:
	SECOND='seconds' |
	MINUTE='minutes' |
	HOUR='hours' |
	DAY='days' |
	WEEK='weeks';

QualifiedName:
	ID ('.' ID)*;

QualifiedNameWithWildcard:
	QualifiedName '.*'?;

terminal DATE:
	'0'..'9' '0'..'9' '0'..'9' '0'..'9' '-' '0'..'9' '0'..'9' '-' '0'..'9' '0'..'9';

terminal REAL returns ecore::EBigDecimal:
	INT '.' INT;

terminal BEGIN:
	'synthetic:BEGIN';

terminal END:
	'synthetic:END';

//@Override
//terminal SL_COMMENT:
//	(' ' | '\t' | '\r' | '\n')* '//' !('\n' | '\r')*;